//	LICENSE
//	This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0. If a copy of the MPL was not
//	distributed with this file, You can obtain one at http://mozilla.org/MPL/2.0/.
//
//	END LICENSE

plugins {
	id 'java-library'
}

repositories {
	mavenCentral()
}

java {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

configurations {
	rhino
	graal
	servlet
}

dependencies {
	rhino 'org.mozilla:rhino:1.7.15'
	graal 'org.graalvm.sdk:graal-sdk:20.3.0'
	servlet 'javax.servlet:javax.servlet-api:3.0.1'

	//	These lines are necessary for VSCode to properly understand the dependencies
	implementation 'org.mozilla:rhino:1.7.15'
	implementation 'org.graalvm.sdk:graal-sdk:20.3.0'
	implementation 'javax.servlet:javax.servlet-api:3.0.1'
}

sourceSets {
	//	'main' is not really the right name for this
	main {
		java {
			srcDir 'rhino/system/java'
			srcDir 'loader/jrunscript/java'
		}
	}

	rhino {
		compileClasspath += configurations.rhino
		compileClasspath += sourceSets.main.output
		java {
			srcDir 'loader/jrunscript/rhino/java'

			//	modules
			srcDir 'jrunscript/host/rhino/java'
			srcDir 'rhino/file/java'
		}
	}

	graal {
		compileClasspath += configurations.graal
		compileClasspath += sourceSets.main.output
		java {
			srcDir 'loader/jrunscript/graal/java'
		}
	}

	servlet {
		compileClasspath += configurations.servlet
		compileClasspath += sourceSets.main.output
		java {
			srcDir 'rhino/http/servlet/java'
		}
	}

	rhinoServlet {
		compileClasspath += configurations.servlet
		compileClasspath += configurations.rhino
		compileClasspath += sourceSets.main.output
		compileClasspath += sourceSets.rhino.output
		compileClasspath += sourceSets.servlet.output
		java {
			srcDir 'rhino/http/servlet/rhino/java'
		}
	}

	jsh {
		compileClasspath += sourceSets.main.output
		java {
			srcDir 'jrunscript/jsh/loader/java'
		}
	}

	jshRhino {
		compileClasspath += configurations.rhino
		compileClasspath += sourceSets.main.output
		compileClasspath += sourceSets.rhino.output
		compileClasspath += sourceSets.jsh.output
		java {
			srcDir 'jrunscript/jsh/loader/rhino/java'
		}
	}

	jshGraal {
		compileClasspath += configurations.graal
		compileClasspath += sourceSets.main.output
		compileClasspath += sourceSets.graal.output
		compileClasspath += sourceSets.jsh.output
		java {
			srcDir 'jrunscript/jsh/loader/graal/java'
		}
	}
}
